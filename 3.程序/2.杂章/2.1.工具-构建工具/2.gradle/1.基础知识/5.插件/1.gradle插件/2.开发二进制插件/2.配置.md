# 配置

大多数插件为构建脚本和其他插件提供一些配置选项，用于自定义插件的工作方式。插件使用 `extension objects` 执行此操作。 Gradle Project 有一个关联的 `ExtensionContainer` 对象，其中包含已应用于项目的插件的所有设置和属性。您可以通过向此容器添加扩展对象来为您的插件提供配置。扩展对象只是一个具有表示配置的 Java Bean 属性的对象。

## 示例

让我们向项目添加一个简单的扩展对象。这里我们向项目添加一个greeting扩展对象，它允许您配置问候语。

### 示例1 自定义插件扩展

kotlin方式：

```kotlin
interface GreetingPluginExtension {
    val message: Property<String>
}

class GreetingPlugin : Plugin<Project> {
    override fun apply(project: Project) {
        // Add the 'greeting' extension object
        val extension = project.extensions.create<GreetingPluginExtension>("greeting")
        extension.message.convention("Hello from GreetingPlugin")
        // Add a task that uses configuration from the extension object
        project.task("hello") {
            doLast {
                println(extension.message.get())
            }
        }
    }
}

apply<GreetingPlugin>()

// Configure the extension
the<GreetingPluginExtension>().message.set("Hi from Gradle")
```

Groovy 方式：

```groovy
// build.gradle
interface GreetingPluginExtension {
    Property<String> getMessage()
}

class GreetingPlugin implements Plugin<Project> {
    void apply(Project project) {
        // Add the 'greeting' extension object
        def extension = project.extensions.create('greeting', GreetingPluginExtension)
        extension.message.convention('Hello from GreetingPlugin')
        // Add a task that uses configuration from the extension object
        project.task('hello') {
            doLast {
                println extension.message.get()
            }
        }
    }
}

apply plugin: GreetingPlugin

// Configure the extension
greeting.message = 'Hi from Gradle'
```

运行：Output of gradle -q hello

```sh
> gradle -q hello
Hi from Gradle
```

在此示例中，`GreetingPluginExtension` 是一个具有名为 message 的属性的对象。扩展对象被添加到名为 greeting 的项目中。然后，此对象可用作与扩展对象同名的项目属性。

通常，您需要在单个插件上指定多个相关属性。 Gradle 为每个扩展对象添加一个配置块，因此您可以将设置组合在一起。以下示例向您展示了这是如何工作的。

### 示例 3.带有配置块的自定义插件

kotlin 方式：

```kotlin
// build.gradle.kts
interface GreetingPluginExtension {
    val message: Property<String>
    val greeter: Property<String>
}

class GreetingPlugin : Plugin<Project> {
    override fun apply(project: Project) {
        val extension = project.extensions.create<GreetingPluginExtension>("greeting")
        project.task("hello") {
            doLast {
                println("${extension.message.get()} from ${extension.greeter.get()}")
            }
        }
    }
}

apply<GreetingPlugin>()

// Configure the extension using a DSL block
configure<GreetingPluginExtension> {
    message.set("Hi")
    greeter.set("Gradle")
}
```

```groovy
// build.gradle
interface GreetingPluginExtension {
    Property<String> getMessage()
    Property<String> getGreeter()
}

class GreetingPlugin implements Plugin<Project> {
    void apply(Project project) {
        def extension = project.extensions.create('greeting', GreetingPluginExtension)
        project.task('hello') {
            doLast {
                println "${extension.message.get()} from ${extension.greeter.get()}"
            }
        }
    }
}

apply plugin: GreetingPlugin

// Configure the extension using a DSL block
greeting {
    message = 'Hi'
    greeter = 'Gradle'
}
```

Output of gradle -q hello

```sh
> gradle -q hello
Hi from Gradle
```

在这个例子中，几个设置可以在 greeting 闭包中组合在一起。构建脚本 (greeting) 中闭包块的名称需要与扩展对象名称相匹配。然后，当执行闭包时，扩展对象上的字段将根据标准 Groovy 闭包委托功能映射到闭包内的变量。

这样，使用扩展对象 extends Gradle DSL 为插件添加项目属性和 DSL 块。因为扩展对象只是一个常规对象，所以您可以通过向扩展对象添加属性和方法来提供嵌套在插件块中的自己的 DSL。

---

## Gradle 扩展对象（Extensions）

在Gradle中，扩展对象是用来为构建脚本提供额外的配置选项的机制。它们允许插件作者定义一组属性和方法，这些可以在项目的`build.gradle`文件中被配置和调用。

以下是对扩展对象的核心内容的描述：

### 1. 定义扩展类

扩展类通常是一个包含属性和方法的Java或Groovy类。这些属性和方法定义了插件的配置界面。

```java
public class MyExtension {
    private String configuration;

    public String getConfiguration() {
        return configuration;
    }

    public void setConfiguration(String configuration) {
        this.configuration = configuration;
    }
}
```

### 2. 在插件中注册扩展

在插件的`apply`方法中，使用`extensions`容器来创建和注册扩展对象。

```groovy
public class MyPlugin implements Plugin<Project> {
    @Override
    public void apply(Project project) {
        // 在项目中注册一个名为'myExtension'的扩展对象
        project.getExtensions().create("myExtension", MyExtension.class);
    }
}
```

### 3. 在`build.gradle`中使用扩展

一旦扩展被注册，用户就可以在`build.gradle`文件中配置它。

```groovy
apply plugin: 'my-plugin'

myExtension {
    configuration = 'someValue'
}
```

### 4. 在插件中访问扩展配置

在插件的`apply`方法或其他Gradle生命周期回调中，可以访问扩展对象的属性。

```groovy
public class MyPlugin implements Plugin<Project> {
    @Override
    public void apply(Project project) {
        MyExtension myExtension = project.getExtensions().getByType(MyExtension.class);
        // 使用myExtension.configuration做一些事情
    }
}
```

### 5. 嵌套扩展

Gradle支持嵌套扩展，允许创建更复杂的配置结构。

```java
public class OuterExtension {
    private InnerExtension inner;

    public InnerExtension getInner() {
        return inner;
    }

    public void setInner(InnerExtension inner) {
        this.inner = inner;
    }

    public static class InnerExtension {
        private String setting;

        public String getSetting() {
            return setting;
        }

        public void setSetting(String setting) {
            this.setting = setting;
        }
    }
}
```

然后在`build.gradle`中这样配置：

```groovy
outerExtension {
    inner {
        setting = 'nestedValue'
    }
}
```

### 6. 查找和获取扩展对象

可以通过名称或类型来查找和获取扩展对象。

```groovy
// 通过名称查找
def extensionByName = project.extensions.findByName('myExtension')

// 通过类型查找
def extensionByType = project.extensions.findByType(MyExtension.class)
```
